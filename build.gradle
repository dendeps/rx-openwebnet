buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

allprojects {
    apply plugin: 'idea'

    group = 'com.github.openwebnet'
    version = '0.3'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'me.tatarka.retrolambda'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    retrolambda {
        jdk System.getenv("JAVA8_HOME")
        oldJdk System.getenv("JAVA7_HOME")
        javaVersion JavaVersion.VERSION_1_7
        exclude 'Test'
    }

    // creates source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // adds javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            MavenJava(MavenPublication) {
                from components.java
                groupId project.group
                artifactId rootProject.name
                version project.version

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        // TODO handle missing variables
        user = BINTRAY_USERNAME
        key = BINTRAY_API_KEY
        publications = ['MavenJava']

        pkg {
            repo = 'maven'
            name = rootProject.name
            websiteUrl = 'https://github.com/openwebnet/rx-openwebnet'
            vcsUrl = 'https://github.com/openwebnet/rx-openwebnet.git'
            licenses = ['MIT']

            version {
                name = project.version
                released = new Date()
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}