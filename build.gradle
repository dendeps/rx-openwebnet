plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.12'
    testCompile 'junit:junit:4.12'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// creates source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// adds javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MavenJava(MavenPublication) {
            from components.java
            groupId 'com.github.niqdev'
            artifactId 'rx-openwebnet'
            version '0.1'
        }
    }
}

bintray {
    user = BINTRAY_USERNAME
    key = BINTRAY_API_KEY
    publications = ['MavenJava']

    pkg {
        repo = 'maven'
        name = 'rx-openwebnet'
        websiteUrl = 'https://github.com/niqdev/rx-openwebnet'
        vcsUrl = 'https://github.com/niqdev/rx-openwebnet.git'
        licenses = ['MIT']

        version {
            name = project.version
            released  = new Date()
            //TODO vcsTag = '1.0'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}