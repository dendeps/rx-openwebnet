buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

allprojects {
    apply plugin: 'idea'

    group = 'com.github.openwebnet'
    version = '1.3.4'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'me.tatarka.retrolambda'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    retrolambda {
        jdk System.getenv("JAVA8_HOME")
        oldJdk System.getenv("JAVA7_HOME")
        javaVersion JavaVersion.VERSION_1_7
    }

    // creates source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    task copyJavaDoc(type: Copy, dependsOn: javadoc) {
        from javadoc.destinationDir
        into("$rootProject.projectDir/javadoc")
    }

    // adds javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            MavenJava(MavenPublication) {
                from components.java
                groupId project.group
                artifactId rootProject.name
                version project.version

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = project.hasProperty('BINTRAY_USERNAME') ? project.BINTRAY_USERNAME : System.getenv('BINTRAY_USERNAME')
        key = project.hasProperty('BINTRAY_API_KEY') ? project.BINTRAY_API_KEY : System.getenv('BINTRAY_API_KEY')
        publications = ['MavenJava']

        pkg {
            repo = 'maven'
            name = rootProject.name
            websiteUrl = 'https://github.com/openwebnet/rx-openwebnet'
            vcsUrl = 'https://github.com/openwebnet/rx-openwebnet.git'
            licenses = ['MIT']

            version {
                name = project.version
                released = new Date()
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
